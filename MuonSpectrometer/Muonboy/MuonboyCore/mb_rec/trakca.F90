!
!> \author M.Virchaux   05/09/96
!
 SUBROUTINE TRAKCA(ID,ISC, CHIKCA,CHIKSA)
 USE M_MB_Control
 USE M_MB_Digis
 USE M_MB_Reco_Tracks
 USE M_MB_General_CONSTANTS
 USE M_MB_MuGeom
 USE M_MB_PACONS
 USE M_MB_CTRABAZ
 USE M_MB_CODRAT
 USE M_MB_Fit
 IMPLICIT NONE
 INTEGER, INTENT(IN)  :: ID, ISC
 REAL(8), INTENT(OUT) :: CHIKCA, CHIKSA
 INTEGER :: IC, IW, IGEOM, IMDT, ILIN, J, NCANDI, ICAN
 INTEGER :: IZTPLA, IDU1,IW01,IDU2,IDU3,IDU4,IDU5, IDD
 REAL(8) :: TTCUR, ZZCUR, SSCUR, RRCUR, FFCUR, EECUR
 REAL(8) :: CF0CUR, SF0CUR, CA0CUR, SA0CUR, PIMIPI
 REAL(8) :: XX, YY, ZZ, RR, FF, EE, AFFDIF, AEEDIF
 REAL(8) :: ZTDROI, SSDROI
 REAL(8) :: RZTCUR(2), RSSCUR(2), CCCCUR(2), CCCSUR(2)
!
   CHIKCA = 9999999.D0
   CHIKSA = 9999999.d0
!
   TTCUR = (TTDRAT(ID,1,ISC)+TTDRAT(ID,2,ISC)) / 2.D0
   ZZCUR = (ZZDRAT(ID,1,ISC)+ZZDRAT(ID,2,ISC)) / 2.D0
   SSCUR = (SSDRAT(ID,1,ISC)+SSDRAT(ID,2,ISC)) / 2.D0
   RRCUR = SQRT( TTCUR**2 + SSCUR**2 )
   FFCUR = FFDRAT(ID,ISC) + ATAN2(SSCUR,TTCUR)
   FFCUR = PIMIPI( FFCUR )
   EECUR = ATAN2(RRCUR,ZZCUR )
   CF0CUR = COS(FFDRAT(ID,ISC))
   SF0CUR = SIN(FFDRAT(ID,ISC))
   CA0CUR = 1.D0
   SA0CUR = 0.D0
   IF(ISC > NMATTR) THEN
     CA0CUR = COS(GGDRAT(ID,ISC))
     SA0CUR = SIN(GGDRAT(ID,ISC))
   ENDIF
   DO J=1,2
     IC = 2 * ISC + 1 - J
     IW = ABS( IWDRAT(ID,J,ISC) )
     IF(IW <= 0) THEN
       IC = 2 * ISC - 2 + J
       IW = ABS( IWDRAT(ID,3-J,ISC) )
     ENDIF
     IGEOM = IGEOMA(IC,IFCHA(IC,IW),IZCHA(IC,IW))
     IMDT = ISTMDTV(IGEOM)
     ILIN = ISTLINV(IGEOM)
     RZTCUR(J) = RESTUB(IMDT)
     RSSCUR(J) = DSLINK(ILIN)
   ENDDO
!
!>> Set common CTRABAZ
   DO J=1,2
     IC = 2 * ISC + 1 - J
     IW = ABS( IWDRAT(ID,J,ISC) )
     CF0PLA(J) = CF0CUR
     SF0PLA(J) = SF0CUR
     CA0PLA(J) = CA0CUR
     SA0PLA(J) = SA0CUR
     IF(IC <= NCOUBA) THEN
       TT0PLA(J) = TTDRAT(ID,J,ISC)
     ELSEIF(IC <= NCOUCH) THEN
       TT0PLA(J) = ZZDRAT(ID,J,ISC)
     ELSE
       TT0PLA(J) = CA0CUR*ZZDRAT(ID,J,ISC) + SA0CUR*TTDRAT(ID,J,ISC)
     ENDIF
     CALL CODEIN(0,0,IW,0,ID,J,IC,0,0,0, J)
   ENDDO
!>> Total number of planes
   NZTPLA = 2
!
!>> Loop on all the "good" candidate tracks already found
   NCANDI = NBCAN
   IF( MBSto == 2 ) NCANDI = NCFIDR
   DO 40 ICAN=1,NCANDI
     IF( MBSto == 2 ) THEN
       XX = VMFIDR(1,ICAN)
       YY = VMFIDR(2,ICAN)
       ZZ = VMFIDR(3,ICAN)
     ELSE
       IF( CHICAN(ICAN) >= FLOOK2 )           CYCLE
       XX = VMUCAN(1,ICAN)
       YY = VMUCAN(2,ICAN)
       ZZ = VMUCAN(3,ICAN)
     ENDIF
     RR = SQRT( XX**2 + YY**2 )
     FF = ATAN2(YY,XX)
     EE = ATAN2(RR,ZZ)
     AFFDIF = ABS(FF-FFCUR)
     AFFDIF = MIN( AFFDIF , ABS(AFFDIF-TWOPI) )
     AEEDIF = ABS(EE-EECUR)
     IF( AFFDIF > 0.2D0 .OR. AEEDIF > 0.2D0 ) CYCLE
!
!>> Set VDEB
     IF( MBSto == 2 ) THEN
       VDEB(1:8,1) = VMFIDR(1:8,ICAN)
     ELSE
       VDEB(1:8,1) = VMUCAN(1:8,ICAN)
     ENDIF
!
!>> Track and catch the crossings
     CALL TRABAZ(1,2,2)
!
!>> Loop on crossings
     CCCCUR(1:2) = 9999999.D0
     CCCSUR(1:2) = 9999999.D0
     DO IZTPLA=1,NZTPLA
       IF( ICRPLA(IZTPLA) <= 0 )  CYCLE
       CALL DECOIN(IZTPLA, IDU1,IW01,IDU2,IDD,J,IC,IDU3,IDU4,IDU5) ! we must have IDD=ID
       IF( IC <= NCOUBA ) THEN
         ZTDROI = ZZDRAT(ID,J,ISC)
       ELSEIF( IC <= NCOUCH ) THEN
         ZTDROI = TTDRAT(ID,J,ISC)
       ELSE
         ZTDROI = -SA0CUR*ZZDRAT(ID,J,ISC) + CA0CUR*TTDRAT(ID,J,ISC)
       ENDIF
       SSDROI = SSDRAT(ID,J,ISC)
       CCCCUR(J) = ((ZTTPLA(IZTPLA,1)-ZTDROI)/RZTCUR(J))**2 + ((SSSPLA(IZTPLA,1)-SSDROI)/RSSCUR(J))**2
       CCCSUR(J) = ((SSSPLA(IZTPLA,1)-SSDROI)/RSSCUR(J))**2
       IF(IW01 <= 0) THEN
         CCCCUR(J) = CCCCUR(J) + 50.D0
         CCCSUR(J) = CCCSUR(J) + 10.D0
       ENDIF
     ENDDO
!
     IF(CCCCUR(1)+CCCCUR(2) < CHIKCA) THEN
       CHIKCA = CCCCUR(1)+CCCCUR(2)
       CHIKSA = CCCSUR(1)+CCCSUR(2)
     ENDIF
40 ENDDO
!
 END SUBROUTINE TRAKCA
!
