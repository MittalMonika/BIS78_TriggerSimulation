#
# File taking care of pointing the downstream projects at the right
# version of the externals.
#

# Find Gaudi:
if( AthSimulation_FIND_QUIETLY )
   find_package( Gaudi REQUIRED QUIET )
else()
   find_package( Gaudi REQUIRED )
endif()

# Temporarily setting additional compile flags here:
add_definitions( -DSIMULATIONBASE )

set( SIMULATIONBASE TRUE CACHE BOOL
   "Flag specifying that this is a simulation release build" )

# Load all the files from the externals/ subdirectory:
get_filename_component( _thisdir ${CMAKE_CURRENT_LIST_FILE} PATH )
file( GLOB _externals "${_thisdir}/externals/*.cmake" )
unset( _thisdir )
foreach( _external ${_externals} )
   include( ${_external} )
   get_filename_component( _extName ${_external} NAME_WE )
   string( TOUPPER ${_extName} _extNameUpper )
   if( NOT AthSimulation_FIND_QUIETLY )
      message( STATUS "Taking ${_extName} from: ${${_extNameUpper}_LCGROOT}" )
   endif()
   unset( _extName )
   unset( _extNameUpper )
endforeach()
unset( _external )
unset( _externals )
